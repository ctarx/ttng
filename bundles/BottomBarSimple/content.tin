#var global_bundle_slots_values[prompt][bottom] {3};
#var zmeczenie[0] {w pelni wypoczety};
#var zmeczenie[1] {wypoczety};
#var zmeczenie[2] {troche zmeczony};
#var zmeczenie[3] {zmeczony};
#var zmeczenie[4] {bardzo zmeczony};
#var zmeczenie[5] {nieco wyczerpany};
#var zmeczenie[6] {wyczerpany};
#var zmeczenie[7] {bardzo wyczerpany};
#var zmeczenie[8] {wycienczony};
#var zmeczenie[9] {calkowicie wycienczony};

#var bbs_fixer 0;

#alias {pasekdolny} {
	#var ppasek $ConfigManager_Configs[pasekdolny];
};

#alias {BottomBarSimple_char_state} {
	#var charstate {%0};
	#foreach {*charstate[]} {s} {
		#list {state} {find} {$s} {v}; 
		#if {$v > 0} {
			#list {state}{set}{$s}{$charstate[$s]}
		} {
			#var {state[$s]} {$charstate[$s]};
		}
	};
	#if {$charstate[fatigue] > 0} {
		#if {"$duch[$avatarid][team]" == "true"} {
			#line sub variables {
				#alias melduj_zmeczenie {
					szepnij ob_$team_lead_id $zmeczenie[$charstate[fatigue]];
				};
			};
			qb melduj_zmeczenie;
		};
	};
	#if {$bbs_fixer > 2} {
		#nop BottomBarClassic;
		BottomBarTinyFugue;
	} {
		#math bbs_fixer {$bbs_fixer + 1};
	};
};

#alias {BottomBarClassic} {
	#nop var global_bundle_slots_values[prompt][bottom] {$Config};
	#draw violet tubed box $screenrows-3 1 $screenrows-1 $screencols {<088>hp: $bbshpbar[$state[hp]] ma: $bbsmanabar[$state[mana]] fa: $bbsstabar[$state[fatigue]] en: $state[encumbrance]       fo: $state[form] im: $state[improve]       in: $state[intox] he: $state[headache] st: $state[stuffed] so: $state[soaked]       pa: $state[panic]};
};

#nop #############################################################;
#nop ############# UKRYWANIE               #######################;
#nop #############################################################;
#var hiddenlevel {${c_stan_niski}off};
#action {Chowasz sie najlepiej jak potrafisz} {
	#var hiddenlevel {${c_stan_sredni} 0%};BottomBarTinyFugue;
	#delay {delayhidden1} {#var hiddenlevel {${c_stan_sredni}33%};BottomBarTinyFugue} {5};
	#delay {delayhidden2} {#var hiddenlevel {${c_stan_sredni}66%};BottomBarTinyFugue} {10};
	#delay {delayhidden3} {#var hiddenlevel {${c_stan_wysoki}on };BottomBarTinyFugue} {15};
};
#action {Nie jestes juz schowany} {
	#undelay {delahidden%*};
	#var hiddenlevel {${c_stan_niski}off};
	BottomBarTinyFugue;
};


#nop #############################################################;
#nop ############# MANEWRY                 #######################;
#nop #############################################################;
#var manewrlevel {${c_stan_wysoki}rdy<088>};
#alias {manewr_start} {
	#var manewrlevel {${c_stan_niski} 5 };RaidBar_refresh;
	#delay {manewrlevel5} {#var manewrlevel {${c_stan_wysoki}rdy<088>};RaidBar_refresh;} {5};
	#delay {manewrlevel4} {#var manewrlevel {${c_stan_sredni} 1 <088>};RaidBar_refresh;} {4};
	#delay {manewrlevel3} {#var manewrlevel {${c_stan_sredni} 2 <088>};RaidBar_refresh;} {3};
	#delay {manewrlevel2} {#var manewrlevel {${c_stan_niski} 3 <088>};RaidBar_refresh;} {2};
	#delay {manewrlevel1} {#var manewrlevel {${c_stan_niski} 4 <088>};RaidBar_refresh;} {1};
};

#nop ROZKAZY;	
#action {Wydajesz rozkaz %1} {
	#line ignore #sh {<168>[  ROZKAZ  ] %0<088>};
	manewr_start;
	#line gag;
};
#action {Twoja wysoka forma pozwala %*} {manewr_start};
#action {Na rozkaz %* zaslaniasz {go|ja} przed ciosami wrogow.} {
	#line ignore #sh {\n<acf>[  ROZKAZ ZASLONA  ] %0<088>\n};
	manewr_start;
	#line gag;
};
#action {Glosno wypowiadasz rozkaz, chyba jednak nikt cie nie zrozumial!} {
	#line ignore #sh {<bad>[ROZKAZ NIEUDANY] %0<088>};
	manewr_start;
	#line gag;
};
#action {Probujesz zaslonic %* przed ciosami %*, jednak nie jestes w stanie tego uczynic.} {
	#line ignore #sh {<bad>[NIE ZASLANIASZ] %0<088>};
	manewr_start;
	#line gag;
};
#action {Nie jestes jeszcze gotow do wykonania kolejnego manewru.} {
	#var bbBGC {<FAA>};
	#delay {manewrDelay} {#var bbBGC {<G02>}} {0.1};
	#line gag;
};

#action {Nie jestes jeszcze gotowa do wykonania kolejnego manewru.} {
	#var bbBGC {<FAA>};
	#delay {manewrDelay} {#var bbBGC {<G02>}} {0.1};#line gag;
};

#action {Nie zaslaniasz nikogo.} {
	#line gag;
};

#action {%* probuje zaslonic %* przed ciosami %*, jednak nie jest w stanie tego uczynic.} {
	#line ignore #sh {<bad>[NIE ZASLANIA] %0<088>};
	#line gag;
} {1};

#action {Zrecznie zaslaniasz %* przed ciosami %*.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
}
#action {Stajesz u boku %1, gotow w kazdej chwili %*} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
}
#action {Z wprawa stajesz pomiedzy %* a %*, przyjmujac na siebie nadchodzace ciosy.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
} {2}
#action {Przestajesz zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJESZ  ] %0<088>};
	#line gag;
}
#action {%* przestaje zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJE  ] %0<088>};
	#line gag;	
}
#action {%* przestaje cie zaslaniac %*} {
	#line ignore #sh {<aaf>[  ZDEJMUJE  ] %0<088>};
	#line gag;	
}
#action {%1 unosi %2 i szybko przesuwa sie za ciebie, kryjac sie przed atakami %3.} {
	#line ignore #sh {\n<acf>[  ZASLANIASZ  ] %0<088>\n};
	manewr_start;
	#line gag;
};
#action {Rzucasz sie na %* przebijajac sie przez %*} {
	#line ignore #sh {\n<fac>[  PRZELAMALES  ] %0<088>\n};
	manewr_start;
	#line gag;
};

#action {%* rzuca sie na %* przebijajac sie przez %*} {
	#format ktozaslonil {%l} {%1};
	#var pokazalem 0;
	#foreach {*duch[]} {ktoto} {
		#if {"$ktozaslonil" == "$duch[$ktoto][desc]"} {
			#if {"$duch[$ktoto][team]" == "true"} {
				#line ignore #sh {<fac>[  PRZELAMAL - POPRAW CEL  ] %0<088>};
				#var pokazalem 1;
			} {
				#nop line ignore #sh {<FAA><178>[ZASLONA] %0<088>};
				#line ignore #sh {<fac>[PRZELAMAL] %0<088>};
				#var pokazalem 1;
			};
		};
	};
	#if {$pokazalem != 1} {
				#line ignore #sh {<fac>[PRZELAMAL] %0<088>};
	};
	#line gag;
}


#action {%* rzuca sie na %* bezskutecznie %*} {
	#line ignore #sh {<bad>[NIE PRZELAMAL] %0<088>};
	manewr_start;
	#line gag;
};


#action {%* zrecznie zaslania %*} {
	#format ktozaslonil {%l} {%1};
	#var pokazalem 0;
	#foreach {*duch[]} {ktoto} {
		#if {"$ktozaslonil" == "$duch[$ktoto][desc]"} {
			#if {"$duch[$ktoto][team]" == "true"} {
				#line ignore #sh {<acf>[  ZASLANIA  ] %0<088>};
				#var pokazalem 1;
			} {
				#nop line ignore #sh {<FAA><178>[ZASLONA] %0<088>};
				#line ignore #sh {<fac>[  ZASLANIA - PRZELAM  ] %0<088>};
				#var pokazalem 1;
			};
		};
	};
	#if {$pokazalem != 1} {
				#line ignore #sh {<acf>[  ZASLANIA  ] %0<088>};
	};
	#line gag;
}
#nop #############################################################;
#nop ############# BLOKOWANIE TWOJE        #######################;
#nop #############################################################;
#alias {blok_start} {
	#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][0]};
		BottomBarTinyFugue;
	#delay {blockselflevel1} {#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][1]<088>};BottomBarTinyFugue;} {1};
	#delay {blockselflevel2} {#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][2]<088>};BottomBarTinyFugue;} {2};
	#delay {blockselflevel3} {#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][3]<088>};BottomBarTinyFugue;} {3};
	#delay {blockselflevel4} {#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][4]<088>};BottomBarTinyFugue;} {4};
};
#action {Przymierzasz sie do odciecia} {blok_start};
#action {Zajmujesz pozycje umozliwiajaca odciecie} {
	#var warninglevel {${c_stan_sredni}     BLOKUJESZ    };
	#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][0]};
	BottomBarTinyFugue;
};
#action {{Przerywasz przygotowania do odciecia|Przestajesz odcinac droge |Udaje ci sie ominac | omija twoj nieskuteczny |Uciekl{e|a}s|przestaje%* odcinac| podazasz}} {
	#var warninglevel {${c_stan_sredni}                  };
	#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][0]};
	#undelay {blockself%*};
	BottomBarTinyFugue;
};

#nop #############################################################;
#nop ############# BLOKOWANIE W CIEBIE     #######################;
#nop #############################################################;
#alias {blok2_start} {
	#undelay {block2%*};
	#var blockselflevel {${c_stan_niski}$TinyFugueBars[blockself][0]};
		BottomBarTinyFugue;
	#delay {block2level1} {#var blockselflevel {${c_stan_niski}$TinyFugueBars[blockself][1]<088>};BottomBarTinyFugue;} {1};
	#delay {block2level2} {#var blockselflevel {${c_stan_niski}$TinyFugueBars[blockself][2]<088>};BottomBarTinyFugue;} {2};
	#delay {block2level3} {#var blockselflevel {${c_stan_niski}$TinyFugueBars[blockself][3]<088>};BottomBarTinyFugue;} {3};
	#delay {block2level4} {#var blockselflevel {${c_stan_niski}$TinyFugueBars[blockself][4]<088>};BottomBarTinyFugue;} {4};
};
#action {przymierza sie do odciecia ci drogi ucieczki} {
	blok2_start;
};
#action {zajmuje pozycje umozliwiajaca odciecie ci drogi ucieczki} {
	#var blockselflevel {${c_stan_wysoki}$TinyFugueBars[blockself][0]};
	#var warninglevel {${c_stan_niski}   ZABLOKOWANY    };
	BottomBarTinyFugue;
};
#action {przestaje odcinac twoja droge ucieczki} {
	#var warninglevel {${c_stan_sredni}                  };BottomBarTinyFugue;
};


#nop #############################################################;
#nop ############# WARNINGS                #######################;
#nop #############################################################;
#action {{Powoli osuwasz sie na ziemie|Potem robi sie ciemno|Sila uderzania zamroczyla cie|czujesz, ze tracisz przytomnosc|Nagle czujesz jak na glowe spada ci ciezki sznur|Nagle czujesz, ze tracisz kontrole nad swym cialem| wali cie na odlew %* chwiejesz|ze swiat przed twoimi oczami niknie|Przymroczony tym uderzeniem czujesz jak nogi|Swiat przed twoimi oczami ciemnieje. Czujesz, ze tracisz wzrok!}} {
	#var warninglevel {${c_stan_sredni}     OGLUSZONY    };BottomBarTinyFugue;
};
#action {{Powoli dochodzisz do siebie|Czujesz jak slabosc po zadanym ciosie w glowe mija|Udaje ci sie uwolnic z sieci|Powoli odzyskujesz swobode ruchow}} {
	#var warninglevel {${c_stan_sredni}                  };BottomBarTinyFugue;
};
#action {{wytraca ci|Uderzenie jest tak silne, ze bezwiednie puszczasz|Czujesz potezne szarpniecie|Bezwladnie opuszczasz}} {
	#var warninglevel {${c_stan_sredni}     WYRTACONY    };BottomBarTinyFugue;
};


#nop #############################################################;
#nop ############# WEAPONS                 #######################;
#nop #############################################################;
#action {{{D|d}obywasz|Wiedziony naglym instynktem siegasz|Pelnym pewnosci siebie ruchem dobywasz| twego pewnego chwytu, | zaciskasz dlon |Trzymasz|dobywajac}} {
	#var weaponlevel {${c_stan_wysoki}on };
	BottomBarTinyFugue;
};
#action {{Opuszczasz | ogarniajac wzrokiem cala okolice| opuszczasz | rozluzniasz uchwyt | do tej pory twe serce }} {
	#var weaponlevel {${c_stan_niski}off};
	BottomBarTinyFugue;
};

#var qbvar {                                                                 };

#var bbBGC {<G02>};
#event {VARIABLE UPDATED bbBGC} {
	BottomBarTinyFugue;	
};

#alias {BottomBarTinyFugue} {
	#var global_bundle_slots_values[prompt][bottom] {3};
	#var hspac {};
	#math {spacelen} {$screencols - 100};
	#loop {1} {$spacelen} {mee} {#var hspac {$hspac }};

	#if {"$ppasek" == "stary"} {
		#line ignore #sh {$bbBGC   <138>$rozk2[nw] $rozk2[n] $rozk2[ne] $rozk2[u]   <088>${bbBGC}K:[$TinyFugueBars[hp][$state[hp]]<088>${bbBGC}] Z:[$TinyFugueBars[fatigue][$state[fatigue]]<088>${bbBGC}] M:[$TinyFugueBars[mana][$state[mana]]<088>${bbBGC}]$hspac Forma : $state[form] Glod: $state[stuffed]<088>} {-4};
		#line ignore #sh {$bbBGC  <138>$rozk2[w]$rozk2[w] o <138>$rozk2[e]$rozk2[e]    <088>${bbBGC}                                                                     $hspac Panika: $state[panic] Prag: $state[soaked]<088>} {-3};
		#line ignore #sh {$bbBGC  <138>$rozk2[sw] $rozk2[s] $rozk2[se]$rozk2[d]   <088>${bbBGC}B:[$qbvar<088>${bbBGC}]$hspac Ciezar: $state[encumbrance] Pija: $state[intox]<088>} {-2};	
	} {
		#line ignore #sh {$bbBGC   <138>$rozk2[nw] $rozk2[n] $rozk2[ne] $rozk2[u]   <088>${bbBGC}K:[$TinyFugueBars[hp][$state[hp]]<088>${bbBGC}] Z:[$TinyFugueBars[fatigue][$state[fatigue]]<088>${bbBGC}] M:[$TinyFugueBars[mana][$state[mana]]<088>${bbBGC}]$hspac Forma : $state[form] Glod: $state[stuffed]<088>} {-4};
		#line ignore #sh {$bbBGC  <138>$rozk2[w]$rozk2[w] o <138>$rozk2[e]$rozk2[e]    <088>${bbBGC}                                                                     $hspac Panika: $state[panic] Prag: $state[soaked]<088>} {-3};
		#line ignore #sh {$bbBGC  <138>$rozk2[sw] $rozk2[s] $rozk2[se]$rozk2[d]   <088>${bbBGC}B:[$qbvar<088>${bbBGC}]$hspac Ciezar: $state[encumbrance] Pija: $state[intox]<088>} {-2};	
		barstest;
	}
};

#alias rysuj_roze {
	#line ignore #sh {$bbBGC   <138>$rozk2[nw] $rozk2[n] $rozk2[ne] $rozk2[u]<088>} {-4} {1};
	#line ignore #sh {$bbBGC  <138>$rozk2[w]$rozk2[w] o <138>$rozk2[e]$rozk2[e]<088>} {-3} {1};
	#line ignore #sh {$bbBGC  <138>$rozk2[sw] $rozk2[s] $rozk2[se]$rozk2[d]<088>} {-2} {1};
};

#alias barstest {
	#math barcols {($screencols - 32) / 3};

	#var hpbar {};
	#math curhp {$state[hp]};
	#math hpslot {($barcols - 2) / 6}; 
	#math hpmodu {($barcols - 2) % 6};
	#math hplen {($curhp * $hpslot) + $hpmodu};
	#loop {1} {$hplen} {hpl} {#var hpbar {$hpbar#};};

	#var stbar {};
	#math curst {9 - $state[fatigue]};
	#math stslot {($barcols - 2) / 9}; 
	#math stmodu {($barcols - 2) % 9};
	#math stlen {($curst * $stslot) + $stmodu};
	#loop {1} {$stlen} {stl} {#var stbar {$stbar#};};

	#var mnbar {};
	#math curmn {$state[mana] + 1};
	#math mnslot {($barcols - 2) / 9}; 
	#math mnmodu {($barcols - 2) % 9};
	#math mnlen {($curmn * $mnslot) + $mnmodu};
	#loop {1} {$mnlen} {mnl} {#var mnbar {$mnbar#};};


	#draw {<088>$bbBGC} jeweled box $screenrows-3 12 $screenrows-1 12+$barcols <afa>$hpbar$bbBGC;
	#draw {<088>$bbBGC} jeweled box $screenrows-3 12+$barcols $screenrows-1 12+$barcols+$barcols <078>$bbBGC$stbar$bbBGC;
	#draw {<088>$bbBGC} jeweled box $screenrows-3 12+$barcols+$barcols $screenrows-1 12+$barcols+$barcols+$barcols <048>$bbBGC$mnbar$bbBGC;
};

#alias zegartest {
	#sh {$bgtop$clockrender[color][10]$clockrender[char][10]<088>$bgtop$clockrender[color][11]$clockrender[char][11]<088>$bgtop$clockrender[color][12]$clockrender[char][12]<088>$bgtop$clockrender[color][1]$clockrender[char][1]<088>$bgbot$clockrender[color][2]$clockrender[char][2]<088>};
	#sh {$bgtop$clockrender[color][9]$clockrender[char][9] $clockhourpane$bgbot   $clockrender[color][3]$clockrender[char][3]<088>};
	#sh {$bgbot$clockrender[color][8]$clockrender[char][8]<088>$bgbot$clockrender[color][7]$clockrender[char][7]<088>$bgbot$clockrender[color][6]$clockrender[char][6]<088>$bgbot$clockrender[color][5]$clockrender[char][5]<088>$bgbot$clockrender[color][4]$clockrender[char][4]<088>$bgbot<088>};
}