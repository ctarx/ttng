#class markups read bundles/ConfigManager/Profiles/markups.tt;

#var markupsStatus off;
#alias {/a %w} {
	#map get roomvnum markupsRoomVnum;
	#map get roomsymbol _rs;
	#map get roomterrain _rt;
	#line sub variables {
		#class markups read bundles/ConfigManager/Profiles/markups.tt;
		#class markups open;

		#var markupsymbols[mob] {<FFA><aaa>MOB<088>};
		#var markupsymbols[ext] {<138> @ <088>};
		#var markupsymbols[rot] {<faa>;~;<088>};
		#if {$m[$markupsRoomVnum][rendered] > 0} {
			#nop line ignore #sh {<178>Highlighter: <038>Juz podswietlam ten pokoj!};
		} {
			#var m[$markupsRoomVnum][rendered] {1};
			#var m[$markupsRoomVnum][type] {%1};
			#var m[$markupsRoomVnum][symbol] {$_rs};
			#var m[$markupsRoomVnum][terrain] {$_rt};
			#map set roomsymbol {$markupsymbols[%1]};
			#line ignore #sh {<178>Highlighter: <028>Dodalem %1};
		};
		#class markups close;
		#class markups write bundles/ConfigManager/Profiles/markups.tt;
	};
} {1};

#alias {/e ext %*} {
	#map get roomvnum markupsRoomVnum;
	#map get roomsymbol _rs;
	#map get roomterrain _rt;
	#line sub variables {
		#class markups read bundles/ConfigManager/Profiles/markups.tt;
		#class markups open;

		#if {$m[$markupsRoomVnum][rendered] > 0} {
			#nop line ignore #sh {<178>Highlighter: <038>Juz podswietlam ten pokoj!};
		} {
			#var m[$markupsRoomVnum][rendered] {1};
			#var m[$markupsRoomVnum][type] {ext};
			#var m[$markupsRoomVnum][direction] {%1};
			#var m[$markupsRoomVnum][symbol] {$_rs};
			#var m[$markupsRoomVnum][terrain] {$_rt};
			#map set roomsymbol {<138> @ <088>};
			#map exit %1 color <138>;
			#line ignore #sh {<178>Highlighter: <028>Dodalem ext %1};
		};
		#class markups close;
		#class markups write bundles/ConfigManager/Profiles/markups.tt;
	};
} {1};

#alias {/gettrolle} {
	#foreach {*m[]} {_m} {
		#if {"$m[$_m][type]" == "mob"} {
			#map find $_m;
			#path save forward trollpath;
			#sh {room:$_m path:$trollpath};
		};
	};
} {1};

#alias {/d} {
	#map get roomvnum markupsRoomVnum;
	#line sub variables {
		#class markups read bundles/ConfigManager/Profiles/markups.tt;
		#class markups open;
		#if {$m[$markupsRoomVnum][rendered] > 0} {
			#map set roomsymbol {$m[$markupsRoomVnum][symbol]};
			#map set roomterrain {$m[$markupsRoomVnum][terrain]}; 
			#unvar m[$markupsRoomVnum];
			#line ignore #sh {<178>Highlighter: <028>Usunalem.};
		} {
			#line ignore #sh {<178>Highlighter: <038>Pokoj nie byl podswietlony!};
		};
		
		#class markups close;
		#class markups write bundles/ConfigManager/Profiles/markups.tt;
	};
} {1};


#var showingmarkups 0;
#alias {/show} {
	#foreach {*m[]} {_m} {
		#if {"$m[$_m][type]" == "rot"} {
			#map at {$_m} {/d};
		};
	};
	#if {$showingmarkups < 1} {
		#foreach {*m[]} {_m} {
			#map at {$_m} {
				#var m[$_m][rendered] {1};
				#map set roomsymbol $markupsymbols[$m[$_m][type]];
				#if {"$m[$_m][type]" == "ext"} {
					#map exit $m[$_m][direction] color <138>;
				};
			};
		};	
		#var showingmarkups 1;
	};
} {1};

#alias {/destroy} {
	#foreach {*m[]} {_m} {
		#map at {$_m} {
			#if {"$m[$_m][type]" == "ext"} {
				#map exit $m[$_m][direction] color;
			};
		};
		#map at {$_m} {/d};		
	};
} {1};

#alias {/hide} {
	#if {$showingmarkups > 0} {
		#foreach {*m[]} {_m} {
			#map at {$_m} {
				#var m[$_m][rendered] {0};
				#map set roomsymbol $m[$_m][symbol];
				#if {"$m[$_m][type]" == "ext"} {
					#map exit $m[$_m][direction] color;
				};
			};
		};
		#var showingmarkups 0;
	};
} {1};

#action {%*% swiata zostalo opanowane przez Ciemnosc.} {
	#var tmpvalue %1;

	#class markups read bundles/ConfigManager/Profiles/markups.tt;
	#class markups open;
	#if {$apka} {
		#if {$tmpvalue < $apka} {
			#line ignore #showme {<138>Podswietlanie: <088>Baza pokojow jest przedawniona. Wpisz <118>/destroy<088> aby usunac stare wpisy.};
		} {
			#list m size msize;
			#line ignore #showme {<138>Podswietlanie: <088>Odswiezam stan apokalipsy. Zaladowane jest: <128>$msize<088> pokojow.};
			#var apka $tmpvalue;
			#unvar msize;				
		}
	} {
		#list m size msize;
		#line ignore #showme {<138>Podswietlanie: <088>Dodaje stan apokalipsy. Zaladowane jest: <128>$msize<088> pokojow.};
		#var apka $tmpvalue;
		#unvar msize;	
	};
	#class markups close;
	#class markups write bundles/ConfigManager/Profiles/markups.tt;
};


#alias {/troll} {
	#var _closestValue 10000;
	#var _closestId 0;

	#path end;
	#list trolle size trollSize;
	#line ignore #sh {<118>Trolli na liscie: <128>$trollSize<088>};
	#if {$trollSize < 1} {
		#line ignore #sh {<118>Buduje baze...};
		#foreach {*m[]} {_m} {
			#if {"$m[$_m][type]" == "mob"} {
				#var trolldistance {0};
				#map find {$_m};
				#path get length trolldistance;
				#var trolle[$_m] $trolldistance;
			};
		};
		#list trolle size trollSize;
		#line ignore #sh {<118>Trolli na liscie: <128>$trollSize<088>};
		#line ignore #sh {<FAA><178>Kolejne wykonanie komendy uruchomi wyszukanie.<088>};
	} {
		#var _closestValue 10000;
		#var _closestId 0;
		#foreach {*trolle[]} {_t} {
			#map find {$_t};
			#path get length trolldistance;
			#if {$trolldistance < $_closestValue} {
				#var _closestId $_t;
				#var _closestValue $trolldistance;
			};
		};
		#if {$_closestId > 0} {
			#line ignore #sh {<138>Najkrotszy dystans to <128> $_closestValue krokow <138> do pokoju <118> $_closestId<088>};
			#map run {$_t} {0.5};
			#nop map find {$_closestId};
			#unvar trolle[$_closestId];
		};
	}
} {1};

/show
